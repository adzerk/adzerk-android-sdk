apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def versionMajor = 2
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 29
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        test.setRoot('src/test')
    }
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
        minGranularity 2
        maxGranularity 3
    }
}

// Bintray package version
version =  "${versionMajor}.${versionMinor}.${versionPatch}"

bintray {
    user = BINTRAY_USER
    key = BINTRAY_API_KEY

    configurations = ['archives']
    pkg {
        userOrg = BINTRAY_USERORG
        repo = BINTRAY_REPO
        name = 'com.adzerk.android.sdk'
        desc = 'Adzerk Android SDK'
        websiteUrl = GITHUB_URL
        issueTrackerUrl = GITHUB_URL + '/issues'
        vcsUrl = GITHUB_GIT
        licenses = [LICENSE_NAME]
        labels = ['aar', 'android', 'library', 'adzerk', 'sdk', 'advertising']
        publicDownloadNumbers = false
    }
}

// Maven groupId
group = 'com.adzerk'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'com.adzerk.android.sdk'
                url GITHUB_URL
                licenses {
                    license {
                        name LICENSE_NAME
                        url LICENSE_URL
                    }
                }
                developers {
                    developer {
                        id  GITHUB_ID
                        name GITHUB_NAME
                        email GITHUB_EMAIL
                    }
                }
                scm {
                    connection GITHUB_GIT
                    developerConnection GITHUB_GIT
                    url GITHUB_URL
                }
            }
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.9'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:2.6.0'
    testImplementation 'org.mockito:mockito-core:2.8.47'
    testImplementation 'org.robolectric:robolectric:3.3.1'
    testImplementation 'com.squareup.assertj:assertj-android:1.1.1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.14.9'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath = configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    title 'Adzerk SDK v' + version
    options.addStringOption('Xdoclint:none', '-quiet')
    exclude 'com/adzerk/android/sdk/rest/AdzerkService.java'
    exclude 'com/adzerk/android/sdk/rest/ContentData.java'
    exclude 'com/adzerk/android/sdk/rest/UserProperties.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompiler.classpath.files
    })
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions {
    doLast {
        println project.getConvention()
    }
}
